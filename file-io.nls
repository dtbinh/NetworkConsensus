;----------------------------------------------------------------------------;
;                                                                            ;
;    NetworkConsensus - FILE I/O MODULE                                      ;
;                                                                            ;
;    Authors:                                                                ;
;        Leonardo Mizoguti                                                   ;
;        Man Hue Tran Diep                                                   ;
;        Thierry Fosso Kenne                                                 ;
;                                                                            ;
;    Coordinator:                                                            ;
;        Ada Diaconescu                                                      ;
;                                                                            ;
;----------------------------------------------------------------------------;


;loads a topology from a file
to load-file [file-name]
  
  if file-name != false [
    
    ; reset model
    clear-all
   
    ; opens file
    file-open file-name
    
    ; get topology settings
    set epsilon file-read
    set head's-value file-read
    set other's-value file-read
    set total-groups file-read
    
    ; setup region
    setup-region
    
    ; group iteration
    set group-id 1
    repeat total-groups [
      let adjacency-matrix file-read
      if adjacency-matrix != nobody [
        set number-of-agents length adjacency-matrix 
        edit-group adjacency-matrix
      ]
      set group-id group-id + 1
    ]
    set group-id 1
    
    ; inter-group links
    let inter-group-links-list file-read
    foreach inter-group-links-list [
      ask turtle (item 0 ?) [ create-influence-link-to turtle (item 1 ?) ]
    ]
    
    file-close
    
    ; adjust the agent values according to their type
    adjust-agent-type-properties
    
    setup-influence-links-weight
    
  ]
    
end

; saves topology into a new file
to save-file [file-name]
    
  ; checks if a file-name has been provided
  if file-name != false [
    
    ; close all files
    file-close-all
    
    ; delete given file if it exists
    if file-exists? file-name [ file-delete file-name ] 
    
    ; creates and open new file
    file-open file-name
    
    ; write topology settings
    file-print (word "; Set epsilon value \n" epsilon "\n")
    file-print (word "; Set head's value \n"  head's-value "\n")
    file-print (word "; Set others' value \n" other's-value "\n")
    file-print (word "; Set total groups \n" total-groups "\n")
    
    ; write adjacency-matrices and inter-group links
    file-print "; For each group, set adjacency matrix \n"
    let id-group 1
    foreach adjacency-matrices [
      file-print word "; Group " id-group
      file-print ?
      file-print ""
      set id-group id-group + 1
    ]
    file-print "; Set inter-group links"
    file-print inter-group-links
    
    ; close file
    file-close
    
  ]
  
end

to print-results-for-random
  let convergence-val (precision ([self-val] of turtle 1) p)
  let influence-report precision ((convergence-val / (head's-value - other's-value))) p
  
  type "convergence value: " print convergence-val
  type "self-weight of agent 0: " print precision ([self-weight] of turtle 0) p
  type "influence-report: " print influence-report
  type "ticks: " print ticks
  
  ; print in a log file
  file-open log-file-path ; open the log file for writing (file path indicated as input)
  if print-log-header[
    file-type "number of agents: " file-type (count turtles)   
    file-print ""
    file-type "epsilon, ticks, convergence-value, influence-report"
    file-print ""
  ]
  file-type epsilon file-type ", "
  file-type ticks file-type ", " 
  file-type convergence-val file-type ", "
  file-type influence-report file-type ", " 
  file-print ""
  file-close
end

; prints the final results after convergence is reached
to print-results
  ;let p 3 ; precision  --> global var
  let convergence-val (precision ([self-val] of turtle 1) p)
  let influence-report precision ((convergence-val / (head's-value - other's-value))) p
  let weight-of-ctrl-agent precision (([[weight] of in-influence-link-from turtle 0] of turtle 1)) p  ; weight of agent 0 on agent 1
  let weight-of-neighs-with-ctrl precision ([get-in-neighbour-weights] of turtle 1) p
  let weight-of-neighs-without-ctrl precision (weight-of-neighs-with-ctrl - weight-of-ctrl-agent) p
  let weight-of-neigh-and-self precision (([self-weight] of turtle 1) + weight-of-neighs-without-ctrl) p
  let weight-report-of-other precision (weight-of-ctrl-agent / weight-of-neigh-and-self) p
  let weight-of-neighs-of-ctrl precision ([get-in-neighbour-weights] of turtle 0) p ; weight of other agents on agent 0 (cotnroller)
  let weight-report-of-ctrl precision (weight-of-neighs-of-ctrl / ([self-weight] of turtle 0)) p
  
  ; print at the output console
  type "convergence value: " print convergence-val
  type "weight of agent 0 on agent 1: " print weight-of-ctrl-agent 
  type "weight of neighbours and self on agent 1: " print weight-of-neigh-and-self
  type "weight of others on agent 0: " print weight-of-neighs-of-ctrl
  type "self-weight of agent 0: " print precision ([self-weight] of turtle 0) p
  type "influence-report: " print influence-report
  type "weight-report of agent 1: " print weight-report-of-other
  type "weight-report of agent 0: " print weight-report-of-ctrl
  type "ticks: " print ticks
  
  ; print in a log file
  file-open log-file-path ; open the log file for writing (file path indicated as input)
  if print-log-header[
    if not printed-head [
      file-type "nb_turtles, epsilon, ticks, convergence-value, weight-of-a0-on-a1, weight-of-neighs-&-self-on-a1, weight-of-others-on-a0, self-weight-of-a0, influence-report, weight-report-of-a1, weight-report-of-a0"
      file-print ""
      set printed-head true
    ]
  ]
  file-type count turtles file-type ", "
  file-type epsilon file-type ", "
  file-type ticks file-type ", " 
  file-type convergence-val file-type ", " 
  file-type weight-of-ctrl-agent file-type ", "  
  file-type weight-of-neigh-and-self file-type ", " 
  file-type weight-of-neighs-of-ctrl file-type ", " 
  file-type precision ([self-weight] of turtle 0) p file-type ", " 
  file-type influence-report file-type ", " 
  file-type weight-report-of-other file-type ", " 
  file-type weight-report-of-ctrl file-type ", " 
  file-print ""
  file-close
end


